title: Brochures
tags: 
colorID: 0
position: -1343,690
---
Mae: Hey şuraya bak, broşürler! #line:a08787
===
title: excuse
tags: 
colorID: 0
position: 639,796
---
Mae: Pardon ama herkes nerede? #line:89e929
Janitor:{width=4} Saat 10:45. Burası kapalı. #line:7da177
Janitor: Bu günlerde Possum Springs'e gelen son otobüsten çok fazla inen olmuyor. #line:9d6e9b
Janitor: Sadece sen. #line:c1a7ed
<<close>>
<<setAnimInt Janitor drilling 1>>
<<wait 2>>
<<set $janitor to 1>>
[[Masada duracak birilerinin olması gerekmiyor mu?|someone]] #line:b64380
[[Peki sen temizlikçi falan mısın?|something]] #line:cf1de5


===
title: how
tags: 
colorID: 0
position: 315,808
---
Mae: Şu kapı işi nasıl gidiyor? -_- #line:e37284
Janitor: Senin soda işi nasıl gidiyor? #line:7acd3b
<<eyes Mae Neutral>>
<<mood Mae Neutral>>
===
title: Janitor
tags: 
colorID: 0
position: 383,500
---
<<if $soda is 0>>
	<<if $janitor is 0>>
		[[excuse]]
	<<elseif $janitor is 1>>
		[[squeeze]]
	<<elseif $janitor is 2>>
		[[how]]
	<<endif>>
<<elseif $soda is 1>>
	[[seeya]]
<<endif>>

===
title: Mural
tags: 
colorID: 0
position: -390,738
---
<<if $bus_station_mural is 0>>
	[[wow]]
<<elseif $bus_station_mural is 1>>
	[[visitors]]
<<elseif $bus_station_mural is 2>>
	[[forest]]
<<elseif $bus_station_mural is 3>>
	[[mine]]
<<endif>>
===
title: Phone
tags: 
colorID: 0
position: -127,63
---
<<if $bus_station_phone is 0>>
	[[reception]]
<<elseif $bus_station_phone is 1>>
	[[whosteals]]
<<endif>>

===
title: seeya
tags: 
colorID: 0
position: 1091,733
---
<<setAnimInt Janitor drilling 2>>
<<wait 1>>
Mae: Bir beleşkola. #line:58f01d
<<close>>
<<trigger Janitor GrabbyHand>>
<<setHoldItem SodaCan>>
<<setGiveFunction JanitorSodaDrink>>
<<trigger Mae Give>>
<<wait 2>>
<<lookRight Mae>>
<<wait 5>>
<<lookNearest Mae>>
Janitor: Mmmh. #line:7c8a0a
Janitor: Ee, sen burada ne yapıyorsun? #line:449b6d
<<setAnimInt Janitor drilling 1>>
Mae: Ben burada yaşıyorum. #line:3bbe4b
Mae: Yani, yaşıyordum. #line:ab4751
Janitor: Hah. Garip. #line:581921
Mae: O kapının işi ne zamana biter sence? #line:7902db
<<close>>
<<setAnimInt Janitor drilling 2>>
<<wait 1>>
Janitor: Şimdi. #line:88ccdb
<<close>>
<<setAnimInt Janitor drilling 0>>
<<wait .75>>
Janitor: Görüşürüz. #line:942e26
<<close>>
<<flip Janitor -1>>
<<walk Janitor ExitLeft>>
<<wait 1>>
<<playOneShot event:/bus_station/bus_station_door BusStationDoor>>
<<waitForMove Janitor>>
<<hide Janitor>>
<<setSceneMood LightsOff>>
<<stopDialogue TV>>
<<closeAll>>
<<animate TV Static>>
<<set $janitor to 5>>
Mae: uh. güle güle. #line:1fe282
===
title: someone
tags: 
colorID: 0
position: 1041,1014
---
Janitor: Kapalı. #line:d1c54a
Mae: Işıklar neden açık? Televizyon neden açık? #line:c1a449
Janitor: Burada tek başıma kalınca korkuyorum. #line:2b734d
Mae: Oh. #line:52d276

===
title: something
tags: 
colorID: 0
position: 600,1155
---
Janitor: Öyle görünüyor. #line:7812bb
Mae: Ne iş yapıyorsun yani? #line:0899b7
Janitor: Bu kapıyı tamir ediyorum. #line:3b29f7
Mae: Bu kadar mı? #line:ae570f
Janitor: Hayır. #line:dfb72e

===
title: squeeze
tags: 
colorID: 0
position: 87,705
---
Mae: Senin yanından sıvışabilir miyim? #line:8b1e12
Janitor: Hayır. Bunu daha şimdi bozdum. #line:320604
Mae: İşin ne zaman biter peki? #line:d1ed74
Janitor: Sen bana makineden bir Fiyaskola getirdikten hemen sonra. #line:981491
Mae: ... #line:ca5497
Mae: Bunu ben mi ödüyorum? #line:dd2baf
Janitor: Ben fazla mesaiye kaldığım zamanlarda makineyi ikna ederim. #line:26a4e0
Mae: İyimiş. Bedava demek mi yani? :) #line:394e29
Janitor: Hayır diyecek kimse olmadığı için evet. #line:f00c90
<<eyes Mae Neutral>>
<<mood Mae Neutral>>
<<set $janitor to 2>>
<<set $vending_machine_done to 0>>
===
title: TicketStand
tags: 
colorID: 0
position: -1306,427
---
Mae: Buarsı bilet standı. #line:a6e72c
===
title: TV
tags: 
colorID: 0
position: -626,417
---
<<animate TV Logo>>
<<wait 5.5>>
<<animate TV MalloyTalk>>
TV: Garbo ve Malloy'a hoşgeldiniz! #line:0cba7f
<<animate TV GarboTalk>>
TV: Bugün haberlerde ne var, Malloy? #line:7ebfe0
<<animate TV MalloyTalk>>
TV: Piyasalar yükselmiş bugün! #line:240660
<<animate TV GarboTalk>>
TV: Fenaaa yükselmiş! #line:ff7908
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_applause TV>>
<<wait 2>>
<<animate TV GarboTalk>>
TV: Grafiğe bakıyorum da, bayağı etkileyici! #line:3c8f6c
<<animate TV MalloyTalk>>
TV: Ekonomi 15,000 yeni iş eklemiş #line:b72c6d
TV: genel olarak grafik işleri #line:50bff0
<<animate TV GarboTalk>>
TV: bunlar da herkesin bildiği gibi gerilemeye dirençlidir! #line:d1750a
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: Aman aman, Garbo, biraz politikleştik! #line:342f8c
<<animate TV GarboTalk>>
TV: Arada sırada politikleşmek gerekir. #line:903a92
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<wait 2>>
<<animate TV MalloyTalk>>
TV: Geçen şu internet randevularından birine çıktım. #line:95e899
<<animate TV GarboTalk>>
TV: Hm? #line:001595
<<animate TV GarboTalk>>
TV: Evet, internet gerçekten de cömert bir sevgiliymiş. #line:df8e7d
<<animate TV GarboStare>>
<<wait 2>>
<<playOneShot event:/bus_station/tv_cough TV>>
<<wait 3>>
<<animate TV GarboTalk>>
TV: Hey, sevgili hanımlardan kendine uygun bir erkek arayan var mı? #line:fceaf6
TV: Bize ulaşın! #line:658baf
<<animate TV GarboLaugh>>
TV: Ben de bulursam size haber veririm! #line:22bd27
<<animate TV MalloyStare>>
<<playOneShot event:/bus_station/tv_laughter_applause TV>>
<<wait 3>>
<<close>>
<<animate TV Static>>
<<wait 3>>

===
title: VendingMachine
tags: 
colorID: 0
position: 539,251
---
<<if $janitor >= 2 and $soda is 0>>
	Mae: O ne? #line:75143a
    Mae: Lime Fiyasko'ları var ve Fiyaskola mı istiyor? #line:3716b2
    Mae: Ne israf ama. #line:058ebb
	<<close>>
	<<wait 1>>
	<<tell VendingMachine to Vend>>
	<<wait 5.5>>
    <<set $can_grab_soda_can to 1>>
    <<set $vending_machine_done to 1>>
    Mae: Tamam, sadece onu almam lazım. #line:94d4e3
	//<<set $soda to 1>>
	//Mae: Got it!
<<else>>
	Mae: :) -_- Fiyasko Tilki, adeta bir rüya gibisin. #line:867df6
    <<eyes Mae Neutral>>
    <<mood Mae Neutral>>
    <<set $vending_machine_done to 1>>
<<endif>>


===
title: wow
tags: 
colorID: 0
position: -765,1046
---
Mae: Vay canına. Bunu buraya ne zaman astılar? #line:07ec71
Mae: Possum Springs hiç bu kadar... #line:ef66b7
Mae: ...yanlış tanıtılmamıştı. :\ #line:585471
<<eyes Mae Neutral>>
<<mood Mae Neutral>>
<<set $bus_station_mural to 1>>
===
title: visitors
tags: 
colorID: 0
position: -562,1273
---
Mae: Ziyaretçiler şehrin gerçek halini görünce hayal kırıklığına uğrayacaklar. #line:48c736
Mae: Buraya gelenler, hayal kırıklığına hazır olun! #line:8702a8
Mae: Şehrimizin Sloganı Bu. #line:e47578
<<set $bus_station_mural to 2>>
===
title: forest
tags: 
colorID: 0
position: -294,1296
---
Mae: Devlet ormanı yukarı, #line:7a46d5
Mae: Normal orman geri kalan her yer. #line:5e929b
Mae: Orman, orman, orman. #line:84a270
<<set $bus_station_mural to 3>>

===
title: mine
tags: 
colorID: 0
position: -114,1046
---
Mae: Şu maden arabalarına da bir bak. #line:1ecaf7
Mae: Hepsi çoktan tarihe karıştı. #line:88f64b
Mae: Bu posteri bir zaman yolcusu falan mı yaptı? #line:e1e5ae
<<set $bus_station_mural_done to 1>>
===
title: reception
tags: 
colorID: 0
position: 78,366
---
Mae: Possum Springs'te telefonların asla çekmemesi operatörlerin işine geliyordur herhalde. #line:8773a7
Mae: Ailemi aramak iyi olurdu #line:a122a1
Mae: ama dallamanın biri telefonu yerinden koparmış. #line:adbeea
Mae: Kim bir telefonu çalar ki? #line:c22b41
<<set $bus_station_phone to 1>>
===
title: whosteals
tags: 
colorID: 0
position: -196,433
---
Mae: Acaba o telefonla şimdi ne yapıyordur? #line:eee3e1
Mae: Geceleri onunla yatıyor. #line:9ba608
Mae: Yürüyüşe çıkarıyor. #line:2752ca
Mae: Sıkı sıkı sarılıyor. #line:3724ad
Mae: İçine doğru konuşuyor olabilir #line:319501
Mae: hiç kimseyle. #line:7a2782
Mae: Ya da belki tam da doğru şekilde onu öpüyor olabilir. #line:4f7c9b
<<set $bus_station_phone_done to 1>>

===
title: EnterLevel
tags: 
colorID: 0
position: 346,-40
---
<<if $busStation is 0>>
	<<set $busStation to 1>>
<<endif>>
<<playerFace -1>>
<<if $janitor >= 5>>
	<<hide Janitor>>
<<else>>
	<<flip Janitor -1>>
	<<setAnimInt Janitor drilling 1>>
<<endif>>
Mae: Ih, bu harika. #line:f53579
Mae: Yani parti falan beklemiyordum ama #line:31f06f
Mae: en azından *birileri* burada olur diye düşünmüştüm. #line:f25ef7
Mae: ... #line:9b299a
Mae: Eve hoşgeldin, Mae. #line:64c8e8
===
